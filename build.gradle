plugins {
    id 'java'
}

def getGitHash = { ->
    try {
        def process = 'git rev-parse --short HEAD'.execute()
        process.waitFor()
        return process.text.trim()
    } catch (Exception e) {
        return "unknown"
    }
}

group = 'org.example'
version = "1.0-${getGitHash()}"

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'Main-Class': 'Main',
                'Implementation-Title': 'VBTest',
                'Implementation-Version': project.version
        )
    }
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    filesMatching('**/*.properties') {
        filter(ReplaceTokens, tokens: [version: project.version.toString()])
    }
}
